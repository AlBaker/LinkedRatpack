apply plugin: 'groovy'

group = 'com.bleedingwolf.ratpack'
version = '0.2-SNAPSHOT'

repositories {
	mavenLocal()
    mavenCentral()
	mavenRepo urls:"http://snapshots.repository.codehaus.org"
}


dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.7.9'
	
	compile ('org.codehaus.groovy.modules.http-builder:http-builder:0.5.0') { 
		exclude(group:'xerces', module: 'xercesImpl')
		exclude(group:'net.sourceforge.nekohtml', module: 'nekohtml')
		//exclude(group:'xml-resolver', module:'xml-resolver')
		exclude(group:'org.codehaus.groovy', module:'groovy')
	}
	
	compile 'org.codehaus.groovy.sparql:groovy-sparql:0.2.1'
    compile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
    compile group: 'org.json', name: 'json', version: '20090211'
    compile(group: 'org.mortbay.jetty', name: 'jetty', version: '6.1.26') { exclude module: 'servlet-api' }
    compile(group: 'org.mortbay.jetty', name: 'jetty-util', version: '6.1.26') { exclude module: 'servlet-api' }
    
    testCompile group: 'junit', name: 'junit', version: '4.8.2'
}

task copyToLib(type: Copy) {
    into "$buildDir/libs"
    from configurations.runtime
}

task deployRatpack(dependsOn: [build,copyToLib], type: Copy) {
    into "${System.properties['user.home']}/.groovy/lib"
    from "$buildDir/libs"
    include "*.jar"
}

task buildDistro(dependsOn: assemble) << {
    ant{
        mkdir(dir:"$buildDir/ratpack/lib")
        mkdir(dir:"$buildDir/ratpack/bin")
    }
    copy{
        from("$projectDir/src/main/binaries")
        into "$buildDir/ratpack/bin"
    }
    ant.chmod(file: "$buildDir/ratpack/bin/ratpack", perm: 'ugo+x')
    copy{
        from configurations.runtime
        into "$buildDir/ratpack/lib"
    }
    copy{
        from "$buildDir/libs" 
        into "$buildDir/ratpack/lib"
        include "Ratpack-*.jar"
    }    
}
